buildscript {
    ext.kotlin_version = '1.1.1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

group = 'com.MasahiroSaito.Spigot'
version = '0.8.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'vault-repo'
        url = 'http://nexus.hc.to/content/repositories/pub_releases'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    compile 'org.spigotmc:spigot-api:1.11.2-R0.1-SNAPSHOT'
    compile 'net.milkbowl.vault:VaultAPI:1.6'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    embed "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

task copyToServer(type: Copy) {
    from "build/libs/Homes-${version}.jar"
    into "server/plugins/"
}

build.finalizedBy copyToServer
